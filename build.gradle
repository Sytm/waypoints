plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'idea'
    id 'java'
}

group = 'de.md5lukas'
version = '2.3'
sourceCompatibility = '1.8'

repositories {
    maven {
        url = 'https://repo.sytm.de/repository/maven-hosted/'
    }

    maven {
        url = 'https://repo.sytm.de/repository/maven-spigot/'
    }

    maven {
        url = 'https://repo.sytm.de/repository/maven-sonatype/'
    }

    maven {
        url = 'https://repo.sytm.de/repository/maven-central/'
    }
}

dependencies {
    implementation ('fr.minuskube.inv:smart-invs:1.3.0-SNAPSHOT') {
        transitive = false
    }

    implementation ('net.wesjd:anvilgui:1.2.3-SNAPSHOT') {
        transitive = false
    }

    compileOnly 'net.md-5:bungeecord-chat:1.13-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    //compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    compileOnly 'de.md5lukas:nbt:1.2.2'
    compileOnly 'de.md5lukas:particle-helper:1.0-SNAPSHOT'
    compileOnly 'de.md5lukas:md5-commons:1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'de.md5lukas:md5-commons:1.1'
    testImplementation 'net.md-5:bungeecord-chat:1.13-SNAPSHOT'
}

shadowJar {
    archiveClassifier.set(null)
    exclude 'META-INF/**'
    relocate 'fr.minuskube.inv', 'de.md5lukas.waypoints.shadow.sinv'
    relocate 'net.wesjd', 'de.md5lukas.waypoints.shadow.wesjd'
    dependencies {
        include(dependency('fr.minuskube.inv:smart-invs'))
        include(dependency('net.wesjd:anvilgui'))
    }
}

test {
    useJUnitPlatform()

    workingDir = projectDir;

    failFast = false
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
